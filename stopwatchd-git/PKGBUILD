# Maintainer: RenoirTan
_pkgname='stopwatchd'
pkgname="${_pkgname}-git"
pkgver=r180.5ae04b5
pkgrel=1
pkgdesc="Stopwatch daemon in the terminal."
arch=('x86_64')
_repo="https://github.com/RenoirTan/Stopwatchd"
url="${_repo}"
license=('custom')
depends=('systemd' 'glibc' 'gcc-libs')
makedepends=('git' 'cargo')
checkdepends=('cargo')
#optdepends=()
provides=("${_pkgname}")
conflicts=("${_pkgname}")
#replaces=()
backup=('etc/stopwatchd/swd.conf')
install='../stopwatchd.install'
_srcdir="Stopwatchd"
source=("${_srcdir}::git+${_repo}.git")
sha256sums=('SKIP')

_vars() {
    export features="default"
    export use_systemd=1
    export prefix='/usr'
}

pkgver() {
    cd "${srcdir}/${_srcdir}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_srcdir}"

    _vars
    ./scripts/build
}

check() {
    cd "${srcdir}/${_srcdir}"

    cargo test --release
}

package() {
    cd "${srcdir}/${_srcdir}"

    _vars
    pkgdir="${pkgdir}" ./scripts/package
}
