# Maintainer: RenoirTan
_pkgname='stopwatchd'
pkgname="${_pkgname}"
pkgver='0.4.1'
pkgrel=1
pkgdesc="Stopwatch daemon in the terminal."
arch=('x86_64')
_repo="https://github.com/RenoirTan/Stopwatchd"
url="${repo}"
license=('custom')
depends=('systemd' 'glibc' 'gcc-libs')
makedepends=('cargo')
checkdepends=('cargo')
#optdepends=()
#provides=()
#conflicts=()
#replaces=()
backup=('etc/stopwatchd/swd.conf')
install='../stopwatchd.install'
_srcdir="Stopwatchd-${pkgver}"
source=("${_repo}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('baab2b7cba7cf85c65e1cb94ab972e7ded46dc65a552d167fab622e3c5791caf')

build() {
    cd "${srcdir}/${_srcdir}"

    cargo build --release
    scripts/mk-systemd-service -p '/usr' -o './out'
}

check() {
    cd "${srcdir}/${_srcdir}"

    cargo test --release
}

package() {
    cd "${srcdir}/${_srcdir}"

    install -Dm 755 './target/release/swd' "${pkgdir}/usr/bin/swd"
    install -Dm 755 './target/release/swctl' "${pkgdir}/usr/bin/swctl"

    install -Dm 644 './out/stopwatchd.service' \
        "${pkgdir}/usr/lib/systemd/system/stopwatchd.service"
    
    install -Dm 644 './README.md' "${pkgdir}/usr/share/doc/${_pkgname}/README.md"

    install -Dm 644 './res/conf/swd.toml' "${pkgdir}/etc/stopwatchd/swd.toml"
}
